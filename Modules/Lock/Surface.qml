pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Effects
import QtQuick.Layouts
import Quickshell.Wayland

import qs.Data
import qs.Components

WlSessionLockSurface {
    id: root

    required property WlSessionLock lock
    required property Pam pam

    property string inputBuffer: ""
    property string maskedBuffer: ""
    property bool showErrorMessage: false
    property bool isAllSelected: false
    readonly property list<string> maskChars: ["6", "7"]

    Connections {
        target: root.lock

        function onUnlock(): void {
            unlockSequence.start();
        }
    }

    Connections {
        target: root.pam
        enabled: root.pam !== null

        function onShowFailureChanged() {
            if (root.pam.showFailure) {
                root.showErrorMessage = true;
                errorShakeAnimation.start();
            }
        }
    }

    StyledRect {
        id: surface

        anchors.fill: parent
        focus: true

        color: Themes.colors.surface_container_lowest

        Keys.onPressed: kevent => {
            // Hide error message on any text input
            if (root.showErrorMessage && kevent.text)
                root.showErrorMessage = false;

            // Handle Enter key - submit password
            if (kevent.key === Qt.Key_Enter || kevent.key === Qt.Key_Return) {
                root.pam.currentText = root.inputBuffer;
                root.pam.tryUnlock();
                root.inputBuffer = "";
                root.maskedBuffer = "";
                root.isAllSelected = false;
                passwordBuffer.color = Themes.colors.on_surface_variant;
                return;
            }

            // Handle Ctrl+A - select all
            if (kevent.key === Qt.Key_A && (kevent.modifiers & Qt.ControlModifier)) {
                if (root.maskedBuffer) {
                    passwordBuffer.color = Themes.colors.blue;
                    root.isAllSelected = true;
                }
                kevent.accepted = true;
                return;
            }

            // Handle Backspace
            if (kevent.key === Qt.Key_Backspace) {
                // Ctrl+Backspace - clear all
                if (kevent.modifiers & Qt.ControlModifier) {
                    root.inputBuffer = "";
                    root.maskedBuffer = "";
                    root.isAllSelected = false;
                    passwordBuffer.color = Themes.colors.on_surface_variant;
                    return;
                }

                // Clear selection or delete last character
                if (root.isAllSelected || root.maskedBuffer) {
                    if (root.isAllSelected) {
                        root.inputBuffer = "";
                        root.maskedBuffer = "";
                        root.isAllSelected = false;
                    } else {
                        root.inputBuffer = root.inputBuffer.slice(0, -1);
                        root.maskedBuffer = root.maskedBuffer.slice(0, -1);
                    }
                    passwordBuffer.color = root.maskedBuffer ? Themes.colors.on_surface : Themes.colors.on_surface_variant;
                }
                return;
            }

            // Handle text input
            if (kevent.text) {
                if (root.isAllSelected) {
                    root.inputBuffer = "";
                    root.maskedBuffer = "";
                    root.isAllSelected = false;
                }

                // Add new character
                root.inputBuffer += kevent.text;
                root.maskedBuffer += root.maskChars[Math.floor(Math.random() * root.maskChars.length)];

                passwordBuffer.color = Themes.colors.on_surface;
                typingAnimation.restart();
            }
        }

        Wallpaper {
            id: wallpaper

            layer.enabled: true
            layer.effect: MultiEffect {
                id: wallBlur

                autoPaddingEnabled: false
                blurEnabled: true

                NumbAnim on blur {
                    duration: Appearance.animations.durations.expressiveDefaultSpatial
                    easing.type: Easing.Linear
                    from: 0
                    to: 0.69
                }
            }
        }

        ColumnLayout {
            id: clockContainer

            anchors.centerIn: parent
            anchors.verticalCenterOffset: -80
            spacing: Appearance.spacing.normal
            opacity: 0
            scale: 0.8
            z: 1

            Clock {}
        }

        StyledLabel {
            id: errorLabel

            anchors.centerIn: parent
            anchors.verticalCenterOffset: -60
            text: "WRONG"
            color: Themes.colors.error
            font.pointSize: Appearance.fonts.large * 5
            opacity: root.showErrorMessage ? 1 : 0
            visible: opacity > 0
            z: 2

            Behavior on opacity {
                NumberAnimation {
                    duration: Appearance.animations.durations.small
                }
            }
        }

        StyledLabel {
            id: passwordBuffer

            anchors.centerIn: parent
            text: root.showErrorMessage ? "" : root.maskedBuffer
            color: root.maskedBuffer ? (root.pam.showFailure ? Themes.colors.on_error_container : Themes.colors.on_surface) : Themes.colors.on_surface_variant
            font.pointSize: Appearance.fonts.extraLarge * 5
            z: 0

            Behavior on color {
                ColAnim {
                    duration: Appearance.animations.durations.small
                }
            }
        }

        ColumnLayout {
            id: sessionContainer

            spacing: Appearance.spacing.normal
            opacity: 0
            scale: 0.8

            anchors.right: parent.right
            anchors.bottom: parent.bottom
            anchors.margins: Appearance.spacing.large

            SessionButton {}
            z: 1
        }
    }

    SequentialAnimation {
        id: unlockSequence

        ParallelAnimation {
            PropertyAnimation {
                target: clockContainer
                property: "opacity"
                from: 1
                to: 0
                duration: Appearance.animations.durations.small
                easing.type: Easing.BezierSpline
                easing.bezierCurve: Appearance.animations.curves.emphasizedDecel
            }
            PropertyAnimation {
                target: clockContainer
                property: "scale"
                from: 1
                to: 0.9
                duration: Appearance.animations.durations.small
                easing.type: Easing.BezierSpline
                easing.bezierCurve: Appearance.animations.curves.emphasizedDecel
            }
            PropertyAnimation {
                target: clockContainer
                property: "anchors.verticalCenterOffset"
                from: -80
                to: -100
                duration: Appearance.animations.durations.small
                easing.type: Easing.BezierSpline
                easing.bezierCurve: Appearance.animations.curves.emphasizedDecel
            }
        }

        PropertyAction {
            target: root.lock
            property: "locked"
            value: false
        }
    }

    SequentialAnimation {
        id: entrySequence
        running: true

        ParallelAnimation {
            PropertyAnimation {
                target: clockContainer
                property: "opacity"
                from: 0
                to: 1
                duration: Appearance.animations.durations.expressiveDefaultSpatial
                easing.type: Easing.BezierSpline
                easing.bezierCurve: Appearance.animations.curves.emphasizedDecel
            }
            PropertyAnimation {
                target: clockContainer
                property: "scale"
                from: 0.9
                to: 1
                duration: Appearance.animations.durations.expressiveDefaultSpatial
                easing.type: Easing.BezierSpline
                easing.bezierCurve: Appearance.animations.curves.emphasizedDecel
            }
            PropertyAnimation {
                target: sessionContainer
                property: "opacity"
                from: 0
                to: 1
                duration: Appearance.animations.durations.expressiveDefaultSpatial
                easing.type: Easing.BezierSpline
                easing.bezierCurve: Appearance.animations.curves.emphasizedDecel
            }
        }
    }

    SequentialAnimation {
        id: errorShakeAnimation

        PropertyAnimation {
            target: errorLabel
            property: "anchors.horizontalCenterOffset"
            from: 0
            to: -8
            duration: Appearance.animations.durations.small * 0.8
            easing.type: Easing.BezierSpline
            easing.bezierCurve: Appearance.animations.curves.expressiveFastSpatial
        }
        PropertyAnimation {
            target: errorLabel
            property: "anchors.horizontalCenterOffset"
            from: -8
            to: 8
            duration: Appearance.animations.durations.small * 0.8
            easing.type: Easing.BezierSpline
            easing.bezierCurve: Appearance.animations.curves.standardAccel
        }
        PropertyAnimation {
            target: errorLabel
            property: "anchors.horizontalCenterOffset"
            from: 8
            to: -4
            duration: Appearance.animations.durations.small * 0.8
            easing.type: Easing.BezierSpline
            easing.bezierCurve: Appearance.animations.curves.standardAccel
        }
        PropertyAnimation {
            target: errorLabel
            property: "anchors.horizontalCenterOffset"
            from: -4
            to: 0
            duration: Appearance.animations.durations.small * 0.8
            easing.type: Easing.BezierSpline
            easing.bezierCurve: Appearance.animations.curves.expressiveFastSpatial
        }
    }

    SequentialAnimation {
        id: typingAnimation

        ParallelAnimation {
            NumbAnim {
                target: passwordBuffer
                property: "scale"
                from: 0.9
                to: 1.0
                duration: Appearance.animations.durations.small
                easing.type: Easing.Linear
                easing.bezierCurve: Appearance.animations.curves.expressiveFastSpatial
            }
            NumbAnim {
                target: passwordBuffer
                property: "opacity"
                from: 0.5
                to: 1.0
                duration: Appearance.animations.durations.small
                easing.type: Easing.Linear
                easing.bezierCurve: Appearance.animations.curves.expressiveFastSpatial
            }
        }
    }
}
