import QtQuick
import qs.Components
import qs.Configs

Text {
    id: root

    enum IconType {
        Lucide,
        Material,
        Weather
    }

    required property string icon
    property int type: Icon.Material

    readonly property string fontFamily: {
        switch (root.type) {
        case Icon.Material:
            return Appearance.fonts.family.material;
        case Icon.Weather:
            return "Weather Icons";
        case Icon.Lucide:
            return "lucide";
        default:
            return Appearance.fonts.family.material;
        }
    }

    font {
        family: root.fontFamily
        pointSize: Appearance.fonts.size.medium
        hintingPreference: Font.PreferNoHinting
        variableAxes: {
            "FILL": false,
            "wght": fontInfo.weight,
            "GRAD": 0,
            "opsz": 48
        }
    }

    antialiasing: true
    color: "transparent"
    renderType: Text.NativeRendering
    text: root.icon

    Behavior on color {
        CAnim {}
    }
}
