pragma ComponentBehavior: Bound

import AnotherRipple
import QtQuick
import Quickshell.Widgets

import qs.Components.Base
import qs.Core.Configs
import qs.Core.Utils
import qs.Services

Item {
    id: root

    property string size: "medium"

    readonly property int actionButtonSize: size === "small" ? 32 : size === "regular" ? 40 : size === "large" ? 96 : 56
    readonly property int actionButtonRadius: size === "small" ? 12 : size === "regular" ? 12 : size === "large" ? 28 : 16
    readonly property int actionButtonIconSize: size === "small" ? 16 : size === "large" ? 36 : 24

    readonly property color backgroundColor: enabled || color.a === 0 ? color : Qt.alpha(color, 0.12)

    property alias backgroundRadius: background.radius
    property bool pressed
    property bool hovered
    property color color: Colours.m3Colors.m3PrimaryContainer
    property IconComponent icon: IconComponent {}

    readonly property bool keyboardFocused: activeFocus

    property bool spinning: false

    onSpinningChanged: {
        if (!spinning) {
            const r = ((iconItem.rotation % 360) + 360) % 360;
            // Snap to normalized angle first so the animation starts from the visible angle.
            iconItem.rotation = r;
            // Shortest path back to 0: go forward to 360 if past halfway, then snap to 0 in onFinished.
            resetAnim.to = r > 180 ? 360 : 0;
            resetAnim.restart();
        } else {
            resetAnim.stop();
        }
    }

    NumberAnimation {
        id: resetAnim

        target: iconItem
        property: "rotation"
        duration: Appearance.animations.durations.normal
        easing.type: Easing.OutCubic
        onFinished: {
            if (iconItem.rotation >= 359.9)
                iconItem.rotation = 0;
        }
    }

    signal clicked

    Keys.onReturnPressed: event => {
        if (enabled) {
            clicked();
            event.accepted = true;
        }
    }

    Keys.onSpacePressed: event => {
        if (enabled) {
            clicked();
            event.accepted = true;
        }
    }

    implicitWidth: actionButtonSize
    implicitHeight: actionButtonSize

    // qmllint disable
    states: [
        State {
            name: "disabled"
            when: !root.enabled
            PropertyChanges {
                target: root
                opacity: 0.38
            }
        },
        State {
            name: "focused"
            when: root.enabled && root.keyboardFocused
            PropertyChanges {
                target: focusRing
                opacity: 1
            }
        },
        State {
            name: "normal"
            when: root.enabled && !root.hovered && !root.pressed && !root.keyboardFocused
        }
    ]
    // qmllint enable

    Elevation {
        visible: root.backgroundColor.a > 0 && root.enabled
        radius: background.radius
        level: root.hovered && !root.pressed ? 4 : 3
    }

    ClippingRectangle {
        id: background

        anchors.fill: parent
        radius: root.enabled && root.pressed ? height * 0.5 : root.actionButtonRadius
        color: root.backgroundColor

        Behavior on radius {
            NAnim {
                duration: Appearance.animations.durations.normal
                easing.type: Easing.OutBack
            }
        }

        SimpleRipple {
            anchors.fill: parent
            color: Colours.m3Colors.m3OnSurfaceVariant
        }

        ParticleRipple {
            anchors.fill: parent
            color: Colours.m3Colors.m3OutlineVariant
            opacity: 0.5
            particleCount: 2
        }
    }

    StateLayer {
        layerEnabled: root.enabled
        layerPressed: root.pressed
        layerHovered: root.hovered

        anchors.fill: parent
        radius: background.radius
        color: root.icon.color
    }

    Rectangle {
        id: focusRing

        anchors.fill: parent
        radius: background.radius
        color: "transparent"
        border.color: Colours.m3Colors.m3Primary
        border.width: 2
        opacity: 0

        Behavior on opacity {
            NAnim {
                duration: Appearance.animations.durations.small
            }
        }
    }

    Icon {
        id: iconItem

        anchors.centerIn: parent
        icon: root.icon.name
        color: root.icon.color
        font.pixelSize: root.icon.size

        RotationAnimator on rotation {
            running: root.spinning
            loops: Animation.Infinite
            duration: Appearance.animations.durations.extraLarge
            easing.type: Easing.Linear
            from: 0
            to: 360
        }
    }

    HoverHandler {
        id: hoverHandler

        cursorShape: root.enabled ? Qt.PointingHandCursor : Qt.ArrowCursor
    }

    hovered: hoverHandler.hovered

    TapHandler {
        id: tapHandler

        enabled: root.enabled
        onTapped: root.clicked()
    }

    pressed: tapHandler.pressed

    component IconComponent: QtObject {
        property color color: Colours.m3Colors.m3OnPrimaryContainer
        property string name: ""
        property int size: root.actionButtonIconSize
    }
}
