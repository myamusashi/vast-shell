pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Wayland
import Quickshell.Hyprland
import Quickshell.Widgets

import qs.Core.States
import qs.Core.Configs
import qs.Components.Base
import qs.Services
import "../captureUtils.js" as Utils

Scope {
    id: root

    required property string screenshotDir

    property string pendingAction: ""
    property string frozenImageUrl: ""
    property bool windowPickerOpen: false
    property string pendingWindowAction: ""
    property real regionScale: 1
    property var pickForRecordCallback: null

    property var allScreenPaths: []
    property var captureDoneCallback: null
    property bool isMultiCapturing: false

    signal notify(string summary, string body, string urgency, string icon, string app, var actions)

    function notifySaved(path): void {
        notify("Screenshot Saved", path, "normal", path, "Screenshot", [
            {
                "id": "open",
                "label": qsTr("Open Image")
            },
            {
                "id": "folder",
                "label": qsTr("Show in Folder")
            }
        ]);
    }

    function screenshotWindow(action) {
        console.log("screenshotWindow: opening window picker, action:", action);
        root.pickForRecordCallback = null;
        root.pendingWindowAction = action || "save+copy";
        root.windowPickerOpen = true;
    }

    function pickWindowForRecord(callback) {
        console.log("pickWindowForRecord: opening window picker for recording");
        root.pickForRecordCallback = callback;
        root.windowPickerOpen = true;
    }

    function screenshotSelection(action) {
        if (GlobalStates.isSelectionOpen)
            return;
        delayTimer.running = false;
        delayTimer.pendingFn = null;

        if (Quickshell.screens.length <= 1) {
            root.pendingAction = "region";
            const screen = Quickshell.screens[0];
            if (!screen) {
                root.notify("Screenshot Failed", "No screen found.", "critical", "dialog-error", "Screenshot");
                return;
            }
            root.regionScale = Hyprland.monitorFor(screen)?.scale ?? 1;
            captureLoader.targetScreen = screen;
            captureLoader.targetWidth = screen.width;
            captureLoader.targetHeight = screen.height;
            delayTimer.interval = 2000;
            delayTimer.pendingFn = () => {
                captureLoader.active = true;
            };
            delayTimer.running = true;
        } else {
            const firstScreen = Quickshell.screens[0];
            root.regionScale = Hyprland.monitorFor(firstScreen)?.scale ?? 1;
            delayTimer.interval = 2000;
            delayTimer.pendingFn = () => {
                root.freezeAllScreens(path => {
                    if (!path) {
                        root.notify("Screenshot Failed", "Failed to capture screens.", "critical", "dialog-error", "Screenshot");
                        return;
                    }
                    root.frozenImageUrl = path;
                    root.selectionOpen = true;
                });
            };
            delayTimer.running = true;
        }
    }

    function screenshotOutput(target, action) {
        delayTimer.running = false;
        delayTimer.pendingFn = null;
        root.pendingAction = action || "save+copy";
        const screen = Quickshell.screens.find(s => s.name === target) ?? Quickshell.screens[0];
        if (!screen) {
            root.notify("Screenshot Failed", "No screen found.", "critical", "dialog-error", "Screenshot");
            return;
        }
        captureLoader.targetScreen = screen;
        captureLoader.targetWidth = screen.width;
        captureLoader.targetHeight = screen.height;
        delayTimer.interval = 2000;
        delayTimer.pendingFn = () => {
            captureLoader.active = true;
        };
        delayTimer.running = true;
    }

    function screenshotAllOutputs(action) {
        delayTimer.running = false;
        delayTimer.pendingFn = null;
        delayTimer.interval = 2000;
        delayTimer.pendingFn = () => {
            root.freezeAllScreens(path => {
                if (!path) {
                    root.notify("Screenshot Failed", "Failed to capture all outputs.", "critical", "dialog-error", "Screenshot");
                    return;
                }
                const srcPath = path.startsWith("file://") ? path.slice(7) : path;
                const outPath = Utils.screenshotPath(root.screenshotDir);
                fileCopyProcess.destPath = outPath;
                fileCopyProcess.command = ["cp", srcPath, outPath];
                fileCopyProcess.running = true;
            });
        };
        delayTimer.running = true;
    }

    function freezeAllScreens(callback) {
        root.allScreenPaths = [];
        root.captureDoneCallback = callback;
        root.pendingCaptureCount = Quickshell.screens.length;
        root.isMultiCapturing = true;
        multiCaptureWatchdog.restart();
    }

    function compositeAllCaptures() {
        multiCaptureWatchdog.stop();
        if (root.allScreenPaths.length === 0) {
            root.isMultiCapturing = false;
            root.notify("Screenshot Failed", "No screens captured.", "critical", "dialog-error", "Screenshot");
            if (root.captureDoneCallback) {
                const cb = root.captureDoneCallback;
                root.captureDoneCallback = null;
                cb("");
            }
            return;
        }
        compositeLoader.active = true;
    }

    function copyToClipboard(img) {
        saver.copyFile(img);
    }

    function getMonitors(callback) {
        const names = Quickshell.screens.map(s => s.name);
        callback(names);
    }

    CaptureSaver {
        id: saver

        screenshotDir: root.screenshotDir

        onSaved: path => root.notifySaved(path)
        onFailed: reason => root.notify("Screenshot Failed", reason, "critical", "dialog-error", "Screenshot")
    }

    Process {
        id: fileCopyProcess

        running: false
        property string destPath: ""
        // qmllint disable
        onExited: (code, status) => {
            // qmllint enable
            if (code === 0 && destPath) {
                root.notifySaved(destPath);
                saver.copyFile(destPath);
            }
            destPath = "";
        }
    }

    LazyLoader {
        id: captureLoader

        property ShellScreen targetScreen: null
        property Toplevel targetToplevel: null
        property int targetWidth: 1
        property int targetHeight: 1

        activeAsync: false

        component: PanelWindow {
            id: captureWindow

            visible: true
            color: "transparent"
            screen: captureLoader.targetScreen
            implicitHeight: captureLoader.targetHeight
            implicitWidth: captureLoader.targetWidth
            exclusionMode: ExclusionMode.Ignore
            WlrLayershell.keyboardFocus: WlrKeyboardFocus.OnDemand
            WlrLayershell.layer: WlrLayer.Overlay

            property bool done: false
            property int grabRetries: 0

            function doGrab() {
                if (screencopyView.width <= 0 || screencopyView.height <= 0) {
                    if (captureWindow.grabRetries < 20) {
                        captureWindow.grabRetries++;
                        grabRetryTimer.restart();
                    } else {
                        console.log("grabToImage: giving up after retries, closing overlays");
                        root.notify("Screenshot Failed", "Capture timed out, please try again.", "critical", "dialog-error", "Screenshot");
                        root.isMultiCapturing = false;
                        root.selectionOpen = false;
                        root.frozenImageUrl = "";
                        captureLoader.active = false;
                        if (root.captureDoneCallback) {
                            const cb = root.captureDoneCallback;
                            root.captureDoneCallback = null;
                            cb("");
                        }
                    }
                    return;
                }

                if (root.isMultiCapturing) {
                    screencopyView.grabToImage(result => {
                        const screen = captureLoader.targetScreen;
                        const path = Utils.tempCapturePath();
                        if (result && result.saveToFile(path)) {
                            root.allScreenPaths.push({
                                screen: screen,
                                path: "file://" + path
                            });
                        } else {
                            console.log("multi capture failed for screen", screen?.name);
                        }
                        captureLoader.active = false;
                    });
                } else if (root.pendingAction === "region") {
                    screencopyView.grabToImage(result => {
                        const path = Utils.tempCapturePath();
                        if (!result) {
                            root.notify("Screenshot Failed", "grabToImage returned null.", "critical", "dialog-error", "Screenshot");
                            captureLoader.active = false;
                            return;
                        }
                        if (result.saveToFile(path)) {
                            root.frozenImageUrl = "file://" + path;
                            captureLoader.active = false;
                            root.selectionOpen = true;
                        } else {
                            root.notify("Screenshot Failed", "Failed to save region preview.", "critical", "dialog-error", "Screenshot");
                            captureLoader.active = false;
                        }
                    });
                } else {
                    console.log("windowCapture: grabbing, source:", captureLoader.targetToplevel ? "toplevel" : "screen", "targetSize:", captureLoader.targetWidth, "x", captureLoader.targetHeight, "viewSize:", screencopyView.width, "x", screencopyView.height);
                    screencopyView.grabToImage(result => {
                        console.log("windowCapture: grabToImage done, result:", !!result, "pendingAction:", root.pendingAction);
                        saver.saveResult(result, root.pendingAction);
                        captureLoader.active = false;
                    });
                }
            }

            Timer {
                id: grabRetryTimer

                interval: 50
                repeat: false
                onTriggered: captureWindow.doGrab()
            }

            ScreencopyView {
                id: screencopyView

                anchors.fill: parent
                captureSource: captureLoader.targetToplevel ?? captureLoader.targetScreen
                live: false
                paintCursor: false

                onHasContentChanged: {
                    if (!hasContent || captureWindow.done)
                        return;
                    captureWindow.done = true;
                    captureWindow.doGrab();
                }
            }
        }
    }

    LazyLoader {
        id: compositeLoader

        activeAsync: false

        property string resultPath: ""

        component: PanelWindow {
            visible: true
            color: "transparent"
            screen: Quickshell.screens[0]
            exclusionMode: ExclusionMode.Ignore
            WlrLayershell.keyboardFocus: WlrKeyboardFocus.OnDemand
            WlrLayershell.layer: WlrLayer.Overlay

            Canvas {
                id: compositeCanvas

                property int imagesToLoad: 0
                property int imagesLoaded: 0

                onImageLoaded: {
                    imagesLoaded++;
                    if (imagesLoaded >= imagesToLoad)
                        requestPaint();
                }

                onPaint: {
                    const ctx = getContext("2d");
                    if (!ctx)
                        return;
                    if (compositeCanvas.width <= 0 || compositeCanvas.height <= 0)
                        return;
                    const screens = root.allScreenPaths;
                    const bounds = Utils.totalBounds(screens.map(entry => entry.screen));
                    ctx.clearRect(0, 0, bounds.width, bounds.height);
                    for (let i = 0; i < screens.length; i++) {
                        const s = screens[i].screen;
                        ctx.drawImage(screens[i].path, s.x - bounds.x, s.y - bounds.y, s.width, s.height);
                    }
                    grabTimer.restart();
                }
            }

            onVisibleChanged: {
                if (!visible)
                    return;
                const screens = root.allScreenPaths;
                if (screens.length === 0) {
                    compositeLoader.active = false;
                    return;
                }
                const bounds = Utils.totalBounds(screens.map(entry => entry.screen));
                compositeCanvas.width = bounds.width;
                compositeCanvas.height = bounds.height;
                compositeCanvas.imagesToLoad = screens.length;
                compositeCanvas.imagesLoaded = 0;
                Qt.callLater(() => {
                    for (let i = 0; i < screens.length; i++)
                        compositeCanvas.loadImage(screens[i].path);
                });
            }

            Timer {
                id: grabTimer

                interval: 150
                repeat: false
                onTriggered: {
                    compositeCanvas.grabToImage(result => {
                        const path = Utils.tempCapturePath();
                        if (result && result.saveToFile(path)) {
                            compositeLoader.resultPath = "file://" + path;
                        }
                        compositeLoader.active = false;
                        root.isMultiCapturing = false;
                        if (root.captureDoneCallback) {
                            const cb = root.captureDoneCallback;
                            root.captureDoneCallback = null;
                            cb(compositeLoader.resultPath);
                        }
                    });
                }
            }
        }
    }

    property bool selectionOpen: false

    Binding {
        target: GlobalStates
        property: "isScreenshotSelectionOpen"
        value: root.selectionOpen
    }

    // Shared selection state in virtual desktop logical pixels
    property point selectionStart: Qt.point(0, 0)
    property point selectionEnd: Qt.point(0, 0)
    property bool selectionDragging: false

    LazyLoader {
        id: cropEngine

        activeAsync: false
        component: PanelWindow {
            visible: true
            exclusionMode: ExclusionMode.Ignore
            WlrLayershell.keyboardFocus: WlrKeyboardFocus.OnDemand
            WlrLayershell.layer: WlrLayer.Overlay
            color: "transparent"

            Image {
                id: cropImage

                source: ""
                sourceClipRect: Qt.rect(0, 0, 0, 0)
                width: sourceClipRect.width > 0 ? sourceClipRect.width : 1
                height: sourceClipRect.height > 0 ? sourceClipRect.height : 1
                cache: false

                onStatusChanged: {
                    if (status === Image.Ready && sourceClipRect.width > 0 && sourceClipRect.height > 0)
                        cropGrabTimer.restart();
                }
            }

            Timer {
                id: cropGrabTimer

                interval: 50
                repeat: false
                onTriggered: {
                    cropImage.grabToImage(result => {
                        const path = Utils.screenshotPath(root.screenshotDir);
                        if (result.saveToFile(path)) {
                            saver.copyFile(path);
                            root.notifySaved(path);
                        } else {
                            root.notify("Screenshot Failed", "Failed to save cropped image.", "critical", "dialog-error", "Screenshot");
                        }
                        cropEngine.active = false;
                        root.selectionOpen = false;
                        root.frozenImageUrl = "";
                    });
                }
            }

            function doCrop(sourceUrl, x, y, w, h) {
                cropImage.sourceClipRect = Qt.rect(x, y, w, h);
                cropImage.source = sourceUrl;
            }
        }
    }

    // Each screen gets its own PanelWindow + ScreencopyView
    // so all outputs freeze at the same compositor frame
    property int pendingCaptureCount: 0

    Timer {
        id: multiCaptureWatchdog

        interval: 4000
        repeat: false
        onTriggered: {
            if (root.isMultiCapturing) {
                console.log("multiCapture watchdog: timed out with", root.pendingCaptureCount, "pending,", root.allScreenPaths.length, "succeeded");
                root.isMultiCapturing = false;
                if (root.allScreenPaths.length === 0) {
                    root.notify("Screenshot Failed", "Capture timed out, please try again.", "critical", "dialog-error", "Screenshot");
                    if (root.captureDoneCallback) {
                        const cb = root.captureDoneCallback;
                        root.captureDoneCallback = null;
                        cb("");
                    }
                } else {
                    root.compositeAllCaptures();
                }
            }
        }
    }

    Variants {
        id: multiCaptureVariants

        model: root.isMultiCapturing ? Quickshell.screens : []

        delegate: PanelWindow {
            id: multiCaptureWindow

            required property ShellScreen modelData

            visible: true
            color: "transparent"
            screen: modelData
            implicitWidth: modelData.width
            implicitHeight: modelData.height
            exclusionMode: ExclusionMode.Ignore
            WlrLayershell.keyboardFocus: WlrKeyboardFocus.None
            WlrLayershell.layer: WlrLayer.Overlay

            property bool multiDone: false
            property int multiGrabRetries: 0

            function multiDoGrab() {
                if (multiCaptureWindow.width <= 0 || multiCaptureWindow.height <= 0) {
                    if (multiCaptureWindow.multiGrabRetries < 20) {
                        multiCaptureWindow.multiGrabRetries++;
                        multiGrabRetryTimer.restart();
                    } else {
                        console.log("multiCapture: giving up on", modelData.name);
                        root.pendingCaptureCount = Math.max(0, root.pendingCaptureCount - 1);
                        if (root.pendingCaptureCount === 0 && root.isMultiCapturing)
                            root.compositeAllCaptures();
                    }
                    return;
                }
                multiScreencopyView.grabToImage(result => {
                    const path = Utils.tempCapturePath();
                    if (result && result.saveToFile(path)) {
                        root.allScreenPaths.push({
                            screen: modelData,
                            path: "file://" + path
                        });
                    } else {
                        console.log("multi capture failed for screen", modelData.name);
                    }
                    root.pendingCaptureCount = Math.max(0, root.pendingCaptureCount - 1);
                    if (root.pendingCaptureCount === 0 && root.isMultiCapturing)
                        root.compositeAllCaptures();
                });
            }

            Timer {
                id: multiGrabRetryTimer

                interval: 50
                repeat: false
                onTriggered: multiCaptureWindow.multiDoGrab()
            }

            ScreencopyView {
                id: multiScreencopyView

                anchors.fill: parent
                captureSource: modelData
                live: false
                paintCursor: false

                onHasContentChanged: {
                    if (!hasContent || multiCaptureWindow.multiDone)
                        return;
                    multiCaptureWindow.multiDone = true;
                    multiCaptureWindow.multiDoGrab();
                }
            }
        }
    }

    Variants {
        id: selectionOverlay

        model: root.selectionOpen ? Quickshell.screens : []

        delegate: PanelWindow {
            required property ShellScreen modelData

            visible: true
            color: "transparent"
            screen: modelData
            exclusionMode: ExclusionMode.Ignore
            WlrLayershell.layer: WlrLayer.Overlay
            WlrLayershell.namespace: "shell:screenshot-overlay"
            WlrLayershell.keyboardFocus: WlrKeyboardFocus.OnDemand

            anchors {
                top: true
                left: true
                right: true
                bottom: true
            }

            readonly property real offsetX: modelData.x
            readonly property real offsetY: modelData.y
            readonly property var frozenBounds: Utils.totalBounds(Quickshell.screens)

            Image {
                source: root.frozenImageUrl
                // Composite starts at the virtual desktop's min corner; offset by
                // it so screens left of / above the primary stay aligned
                x: frozenBounds.x - offsetX
                y: frozenBounds.y - offsetY
                width: frozenBounds.width
                height: frozenBounds.height
                cache: false
                fillMode: Image.Pad
            }

            Rectangle {
                anchors.fill: parent
                color: Qt.alpha(Colours.m3Colors.m3Background, 0.5)
            }

            Rectangle {
                visible: root.selectionDragging
                x: Math.min(root.selectionStart.x, root.selectionEnd.x) - offsetX
                y: Math.min(root.selectionStart.y, root.selectionEnd.y) - offsetY
                width: Math.abs(root.selectionEnd.x - root.selectionStart.x)
                height: Math.abs(root.selectionEnd.y - root.selectionStart.y)
                color: "transparent"
                border.color: Colours.m3Colors.m3OnSurface
                border.width: 2

                Rectangle {
                    anchors.fill: parent
                    color: Qt.alpha(Colours.m3Colors.m3OnSurface, 0.25)
                }
            }

            Item {
                id: focusCatcher

                anchors.fill: parent
                focus: root.selectionOpen

                Keys.onEscapePressed: {
                    root.selectionOpen = false;
                    root.frozenImageUrl = "";
                }
                Component.onCompleted: forceActiveFocus()
            }

            Timer {
                id: overlayWatchdog

                interval: 30000
                repeat: false
                running: root.selectionOpen
                onTriggered: {
                    console.log("selectionOverlay watchdog: force-closing frozen overlay");
                    root.selectionOpen = false;
                    root.frozenImageUrl = "";
                }
            }

            MouseArea {
                anchors.fill: parent
                cursorShape: Qt.CrossCursor
                acceptedButtons: Qt.LeftButton | Qt.RightButton

                onPressed: e => {
                    if (e.button === Qt.RightButton) {
                        root.selectionOpen = false;
                        root.frozenImageUrl = "";
                        return;
                    }
                    root.selectionStart = Qt.point(e.x + offsetX, e.y + offsetY);
                    root.selectionEnd = root.selectionStart;
                    root.selectionDragging = true;
                }
                onPositionChanged: e => {
                    if (root.selectionDragging)
                        root.selectionEnd = Qt.point(e.x + offsetX, e.y + offsetY);
                }
                onReleased: e => {
                    if (!root.selectionDragging)
                        return;
                    root.selectionDragging = false;

                    const selectionMinX = Math.min(root.selectionStart.x, root.selectionEnd.x);
                    const selectionMinY = Math.min(root.selectionStart.y, root.selectionEnd.y);
                    const selectionWidth = Math.abs(root.selectionEnd.x - root.selectionStart.x);
                    const selectionHeight = Math.abs(root.selectionEnd.y - root.selectionStart.y);

                    if (selectionWidth < 5 || selectionHeight < 5) {
                        root.selectionOpen = false;
                        root.frozenImageUrl = "";
                        return;
                    }

                    // Crop coords are relative to the composite's min corner
                    const bounds = Utils.totalBounds(Quickshell.screens);
                    const scale = root.regionScale;
                    const cropX = Math.round((selectionMinX - bounds.x) * scale);
                    const cropY = Math.round((selectionMinY - bounds.y) * scale);
                    const cropWidth = Math.round(selectionWidth * scale);
                    const cropHeight = Math.round(selectionHeight * scale);

                    cropEngine.active = true;
                    cropEngine.item.doCrop(root.frozenImageUrl, cropX, cropY, cropWidth, cropHeight);
                }
            }
        }
    }

    Timer {
        id: delayTimer

        repeat: false
        property var pendingFn: null
        onTriggered: {
            if (pendingFn) {
                const fn = pendingFn;
                pendingFn = null;
                fn();
            }
        }
    }

    LazyLoader {
        id: windowPicker

        activeAsync: root.windowPickerOpen

        component: PanelWindow {
            id: pickerWindow

            visible: true
            exclusionMode: ExclusionMode.Ignore
            WlrLayershell.keyboardFocus: WlrKeyboardFocus.OnDemand
            WlrLayershell.layer: WlrLayer.Overlay

            anchors {
                top: true
                left: true
                right: true
                bottom: true
            }
            color: "transparent"

            Item {
                id: pickerFocus

                anchors.fill: parent
                focus: true
                Keys.onEscapePressed: {
                    root.pickForRecordCallback = null;
                    root.windowPickerOpen = false;
                }
                Component.onCompleted: forceActiveFocus()
            }

            Rectangle {
                anchors.fill: parent
                color: Qt.alpha(Colours.m3Colors.m3Background, 0.6)

                MouseArea {
                    anchors.fill: parent
                    onClicked: {
                        root.pickForRecordCallback = null;
                        root.windowPickerOpen = false;
                    }
                }
            }

            property real pickerOriginX: 0
            property real pickerOriginY: 0

            // Scrolling layouts place clients at negative or far-positive global
            // coords; the active-workspace union bounds are fitted into the
            // viewport so every window stays visible and clickable.
            property real pickerScale: 1

            function recalcPickerTransform() {
                let minX = Infinity, minY = Infinity;
                let maxX = -Infinity, maxY = -Infinity;
                const toplevels = Hypr.toplevels;
                for (let i = 0; i < toplevels.length; i++) {
                    if (toplevels[i].workspace?.id !== Hypr.activeWsId)
                        continue;
                    const ipc = toplevels[i].lastIpcObject;
                    const at = ipc?.at;
                    const size = ipc?.size;
                    if (!at || !size || size[0] <= 0 || size[1] <= 0)
                        continue;
                    minX = Math.min(minX, at[0]);
                    minY = Math.min(minY, at[1]);
                    maxX = Math.max(maxX, at[0] + size[0]);
                    maxY = Math.max(maxY, at[1] + size[1]);
                }
                if (minX === Infinity) {
                    pickerOriginX = 0;
                    pickerOriginY = 0;
                    pickerScale = 1;
                    return;
                }
                const margin = Appearance.margin.normal * 2;
                const spanWidth = Math.max(1, maxX - minX);
                const spanHeight = Math.max(1, maxY - minY);
                // Never upscale — ordinary layouts keep 1:1 geometry
                pickerScale = Math.min(1, Math.max(1, pickerWindow.width - margin * 2) / spanWidth, Math.max(1, pickerWindow.height - margin * 2) / spanHeight);
                pickerOriginX = minX;
                pickerOriginY = minY;
            }

            Timer {
                id: pickerRefreshTimer

                interval: 400
                repeat: false
                onTriggered: {
                    Hyprland.refreshToplevels();
                    pickerRecalcTimer.restart();
                }
            }

            Connections {
                target: Hyprland
                function onRawEvent(event) {
                    const eventName = event.name;
                    if (["movewindow", "openwindow", "closewindow", "changefloatingmode"].includes(eventName))
                        pickerRefreshTimer.restart();
                }
            }

            // refreshToplevels() lands over IPC, recalc once fresh geometry arrived
            Timer {
                id: pickerRecalcTimer

                interval: 150
                repeat: false
                onTriggered: recalcPickerTransform()
            }

            Connections {
                target: Hypr

                function onToplevelsChanged() {
                    pickerRecalcTimer.restart();
                }
            }
            Component.onCompleted: {
                recalcPickerTransform();
                pickerRefreshTimer.start();
            }
            Repeater {
                id: pickerRepeater

                model: Hypr.toplevels

                delegate: Rectangle {
                    id: pickerDelegate

                    required property HyprlandToplevel modelData

                    readonly property var ipc: modelData.lastIpcObject

                    x: ((ipc?.at?.[0] ?? 0) - pickerOriginX) * pickerScale
                    y: ((ipc?.at?.[1] ?? 0) - pickerOriginY) * pickerScale
                    width: (ipc?.size?.[0] ?? 0) * pickerScale
                    height: (ipc?.size?.[1] ?? 0) * pickerScale
                    visible: width > 0 && height > 0 && modelData.workspace?.id === Hypr.activeWsId
                    z: modelData.focusHistoryID
                    color: pickerMouse.containsMouse ? Qt.lighter(Colours.m3Colors.m3Primary, 1.4) : Colours.m3Colors.m3Primary
                    opacity: pickerMouse.containsMouse ? 0.55 : 0.25
                    radius: 6
                    border.color: pickerMouse.containsMouse ? Colours.m3Colors.m3OnPrimary : "transparent"
                    border.width: 3

                    Column {
                        anchors.centerIn: parent
                        spacing: Appearance.spacing.small
                        width: Math.min(parent.width - Appearance.margin.normal * 2, 300)
                        IconImage {
                            anchors.horizontalCenter: parent.horizontalCenter
                            source: Quickshell.iconPath(DesktopEntries.heuristicLookup(modelData.lastIpcObject?.class)?.icon, "image-missing")
                            asynchronous: true
                            width: 32
                            height: 32
                            backer.cache: true
                        }
                        StyledText {
                            anchors.horizontalCenter: parent.horizontalCenter
                            text: modelData.lastIpcObject?.class ?? modelData.title ?? "?"
                            color: Colours.m3Colors.m3OnPrimary
                            font.pixelSize: Appearance.fonts.size.normal
                            font.bold: true
                            maximumLineCount: 2
                            width: parent.width
                            wrapMode: Text.WordWrap
                            horizontalAlignment: Text.AlignHCenter
                        }
                    }

                    Rectangle {
                        anchors {
                            left: parent.left
                            right: parent.right
                            bottom: parent.bottom
                        }
                        height: Appearance.spacing.large
                        color: Qt.alpha(Colours.m3Colors.m3Scrim, 0.6)
                        radius: Appearance.rounding.small
                        visible: pickerMouse.containsMouse
                        StyledText {
                            anchors.centerIn: parent
                            text: Math.round(pickerDelegate.x) + "," + Math.round(pickerDelegate.y) + "  " + Math.round(pickerDelegate.width) + "×" + Math.round(pickerDelegate.height)
                            color: Colours.m3Colors.m3OnSurface
                            font.pixelSize: Appearance.fonts.size.small
                        }
                    }

                    MouseArea {
                        id: pickerMouse

                        anchors.fill: parent
                        hoverEnabled: true
                        cursorShape: Qt.PointingHandCursor
                        onClicked: {
                            if (root.pickForRecordCallback) {
                                const appId = modelData.lastIpcObject?.class;
                                const cb = root.pickForRecordCallback;
                                root.pickForRecordCallback = null;
                                root.windowPickerOpen = false;
                                cb(appId);
                                return;
                            }
                            const ipc = modelData.lastIpcObject;
                            const size = ipc?.size ?? [0, 0];
                            const workspace = Hypr.focusedWorkspace;
                            const monitor = workspace?.monitor;
                            const screen = monitor ? (Quickshell.screens.find(s => s.name === monitor.name) ?? Quickshell.screens[0]) : Quickshell.screens[0];
                            root.pendingAction = root.pendingWindowAction;
                            captureLoader.targetScreen = screen;
                            captureLoader.targetToplevel = modelData.wayland;
                            captureLoader.targetWidth = size[0] || 1;
                            captureLoader.targetHeight = size[1] || 1;
                            captureLoader.active = true;
                            root.windowPickerOpen = false;
                        }
                    }
                }
            }
        }
    }
}
