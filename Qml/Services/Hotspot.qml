pragma ComponentBehavior: Bound
pragma Singleton

import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Networking

import qs.Services

Singleton {
    id: root

    enum Status {
        Inactive = 0,
        Starting = 1,
        Active = 2,
        Stopping = 3,
        ErrorStatus = 4
    }

    property string band: ""
    property int channel: 6
    property string errorMessage: ""

    // prefer a active device as hotspot interface
    readonly property string hotspotInterface: wifiDevicePicker.hotspotInterface
    readonly property bool isActive: status === Hotspot.Status.Active
    property string password: ""
    property string ssid: ""
    property int status: Hotspot.Status.Inactive

    // Prefer a connected ethernet device as upstream
    readonly property string upstreamInterface: SystemUsage.allEthernetDevices

    function setError(msg) {
        errorMessage = msg;
        status = Hotspot.Status.ErrorStatus;
        console.warn("[Hotspot] Error:", msg);
        ToastService.show(qsTr("[Hotspot] Error: %1").arg(msg), qsTr("Hotspot"), "network-wireless-hotspot-symbolic", 3000);
    }
    function start() {
        if (status === Hotspot.Status.Active || status === Hotspot.Status.Starting)
            return;
        if (!hotspotInterface) {
            setError("No wireless interface available");
            return;
        }

        // Apply defaults at start time, not at bind time
        const ssid = ssid || "Quickshell";
        const password = password || "password123";
        const band = band || "bg";
        const channel = channel || 6;

        status = Hotspot.Status.Starting;
        errorMessage = "";
        createHotspot.command = ["bash", "-c", `nmcli con delete "Hotspot" 2>/dev/null; ` + `nmcli con add type wifi ifname ${hotspotInterface} ` + `con-name Hotspot autoconnect no ssid "${ssid}" ` + `mode ap ipv4.method shared ` + `wifi-sec.key-mgmt wpa-psk ` + `wifi-sec.psk "${password}" ` + `wifi.band ${band} ` + `wifi.channel ${channel}`];
        createHotspot.running = true;
    }
    function stop() {
        if (status !== Hotspot.Status.Active)
            return;
        status = Hotspot.Status.Stopping;
        stopHotspot.running = true;
    }
    function toggle() {
        isActive ? stop() : start();
    }

    Component.onCompleted: {
        ToastService.show(qsTr("Upstream Interface: %1").arg(upstreamInterface), qsTr("Hotspot"), "network-wireless-hotspot-symbolic", 3000);
        queryStatus.running = true;
    }

    Instantiator {
        id: wifiDevicePicker

        readonly property string hotspotInterface: {
            for (let i = 0; i < count; i++) {
                const item = objectAt(i);
                // qmllint disable
                if (item && item.isWifi && item.ifname)
                    return item.ifname;
                // qmllint enable
            }
            return "";
        }

        model: Networking.devices

        delegate: QtObject {
            // qmllint enable
            readonly property string ifname: isWifi ? (modelData.name ?? "") : ""

            // qmllint disable
            readonly property bool isWifi: modelData.type === DeviceType.Wifi
            required property NetworkDevice modelData
        }
    }
    Process {
        id: createHotspot

        command: []

        stderr: StdioCollector {
            id: stdCreateHotspot
        }

        // qmllint disable
        onExited: code => {
            // qmllint enable
            if (code !== 0) {
                root.setError("Failed to create hotspot connection: " + stdCreateHotspot.text);
                return;
            }
            startHotspot.running = true;
        }
    }
    Process {
        id: startHotspot

        command: ["nmcli", "con", "up", "Hotspot"]

        stderr: StdioCollector {
            id: stdStartHotspot
        }

        // qmllint disable
        onExited: code => {
            // qmllint enable
            if (code !== 0) {
                root.setError("Failed to bring up hotspot: " + stdStartHotspot.text);
                return;
            }
            root.status = Hotspot.Status.Active;
            console.info("[Hotspot] Active on", root.hotspotInterface, "| SSID:", root.ssid);
            ToastService.show(qsTr("[Hotspot] Active on %1 | SSID: %2").arg(root.hotspotInterface).arg(root.ssid), qsTr("Hotspot"), "network-wireless-hotspot-symbolic", 3000);
        }
    }
    Process {
        id: stopHotspot

        command: ["bash", "-c", "nmcli con down Hotspot; nmcli con delete Hotspot"]

        stderr: StdioCollector {
            id: stdStopHotspot
        }

        // qmllint disable
        onExited: code => {
            // qmllint enable
            if (code !== 0) {
                console.warn("[Hotspot] Stop exited with code", code, stdStopHotspot.text);
                ToastService.show(qsTr("[Hotspot] Stop exited with code %1: %2").arg(code).arg(stdStopHotspot.text), qsTr("Hotspot"), "network-wireless-hotspot-symbolic", 3000);
            }
            root.status = Hotspot.Status.Inactive;
            console.info("[Hotspot] Hotspot stopped");
            ToastService.show(qsTr("Hotspot stopped"), qsTr("Hotspot"), "network-wireless-hotspot-symbolic", 3000);
        }
    }
    Process {
        id: queryStatus

        command: ["nmcli", "-t", "-f", "NAME,STATE", "con", "show", "--active"]

        stderr: StdioCollector {
        }
        stdout: StdioCollector {
            id: stdQueryStatus

            onStreamFinished: {
                const data = text.trim();
                if (data.split("\n").some(line => line.startsWith("Hotspot:")))
                    root.status = Hotspot.Status.Active;
            }
        }

        // qmllint disable
        onExited: code => {
            // qmllint enable
            if (code !== 0) {
                console.warn("[Hotspot] Status query failed (", code, "):", stdQueryStatus.text);
                root.status = Hotspot.Status.Inactive;
            }
        }
    }
}
